# cmake uninstall template. For details see:
# https://gitlab.kitware.com/cmake/community/-/wikis/FAQ#can-i-do-make-uninstall-with-cmake

if(NOT EXISTS "@CMAKE_BINARY_DIR@/install_manifest.txt")
  message(FATAL_ERROR "Cannot find install manifest: @CMAKE_BINARY_DIR@/install_manifest.txt")
endif()

file(READ "@CMAKE_BINARY_DIR@/install_manifest.txt" files)
string(REGEX REPLACE "\n" ";" files "${files}")

foreach(file ${files})
  message(STATUS "Uninstalling $ENV{DESTDIR}${file}")
  if(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    execute_process(
      COMMAND "@CMAKE_COMMAND@" -E remove "$ENV{DESTDIR}${file}"
      OUTPUT_VARIABLE rm_out
      RESULT_VARIABLE rm_retval
      )
    if(NOT "${rm_retval}" STREQUAL 0)
      message(FATAL_ERROR "Problem when removing $ENV{DESTDIR}${file}")
    endif()
  else(IS_SYMLINK "$ENV{DESTDIR}${file}" OR EXISTS "$ENV{DESTDIR}${file}")
    message(STATUS "File $ENV{DESTDIR}${file} does not exist.")
  endif()
endforeach()

set(FLEXEMU_POST_INSTALL @FLEXEMU_POST_INSTALL@)
if(UNIX AND FLEXEMU_POST_INSTALL)
    string(FIND "@XDG_UPDATE_DESKTOP_DATABASE@" "NOTFOUND" STR_INDEX)
    if (${STR_INDEX} EQUAL -1)
        execute_process(COMMAND echo "-- Updating desktop database")
        execute_process(COMMAND "@XDG_UPDATE_DESKTOP_DATABASE@" "@APPLICATIONS_DIR@")
    endif()

    string(FIND "@XDG_UPDATE_MIME_DATABASE@" "NOTFOUND" STR_INDEX)
    if (${STR_INDEX} EQUAL -1)
        execute_process(COMMAND echo "-- Updating mime database")
        execute_process(COMMAND "@XDG_UPDATE_MIME_DATABASE@" "@MIME_DIR@")
    endif()

    string(FIND "@GTK_UPDATE_ICON_CACHE@" "NOTFOUND" STR_INDEX)
    if (${STR_INDEX} EQUAL -1)
        execute_process(COMMAND echo "-- Updating icon cache")
        execute_process(COMMAND "@GTK_UPDATE_ICON_CACHE@" -f -t -q "@HICOLOR_DIR@")
    endif()
endif()

